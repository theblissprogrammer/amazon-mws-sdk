apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-allopen'
apply plugin: 'com.github.dcendents.android-maven'

group = 'com.github.theblissprogrammer'

allOpen {
    // allows mocking for classes w/o directly opening them for release builds
    annotation 'com.android.example.github.testing.OpenClass'
}

android {
    compileSdkVersion build_versions.target_sdk

    defaultConfig {
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas".toString())
            }
        }
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    sourceSets {
        androidTest.java.srcDirs += "src/test-common/java"
        test.java.srcDirs += "src/test-common/java"
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }

    // add the schema location to the source sets
    // used by Room, to test migrations
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation deps.support.app_compat
    implementation deps.support.broadcast_manager
    implementation deps.multidex
    implementation deps.room.runtime

    kapt deps.room.compiler
    kapt deps.lifecycle.compiler

    implementation deps.okhttp.runtime

    implementation 'commons-codec:commons-codec:1.11'
    implementation 'org.jsoup:jsoup:1.11.3'

    implementation 'com.amazonaws:aws-android-sdk-sqs:2.16.0'

    implementation deps.kotlin.stdlib
    implementation deps.coroutines.runtime
    implementation deps.coroutines.core

    androidTestImplementation deps.arch_core.testing
    androidTestImplementation deps.espresso.contrib
    androidTestImplementation deps.espresso.core
    androidTestImplementation deps.espresso.intents
    testImplementation deps.junit
}

// we need all open to run tests which  a we enable only for debug builds.
project.tasks.whenTaskAdded {
    if (it.name == "testReleaseUnitTest") {
        it.enabled = false
    }
}